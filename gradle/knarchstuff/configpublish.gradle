apply plugin: 'maven-publish'

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "Square"
            name "Square"
            organization "Square"
            organizationUrl "https://square.com/"
        }
    }
    scm {
        url "https://github.com/square/sqldelight"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

def art = project.hasProperty("POM_ARTIFACT_ID") ? project.property("POM_ARTIFACT_ID") : project.name
version = VERSION_NAME
group = GROUP

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId art
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            url (project.hasProperty('j2objcMavenDeploy') ? project.property('j2objcMavenDeploy') : 'file://nope/')
        }
    }
}

afterEvaluate {
    project.publishing.publications.forEach { publication ->
        println("pub.groupId ${publication.groupId}, artifactId ${publication.artifactId}, publication ${publication}")
        publication.pom.withXml {
            def root = asNode()
            root.appendNode('name', project.name)
            root.appendNode('description', 'SqlDelight')
            root.appendNode('url', 'https://github.com/square/sqldelight')
            root.children().last() + pomConfig
        }
    }
}